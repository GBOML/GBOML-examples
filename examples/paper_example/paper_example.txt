#TIMEHORIZON
T = 8760; // planning horizon (hours)

#NODE SOLAR_PV
#PARAMETERS
capex = 600; // annualised capital expenditure per unit capacity
capacity_factor = import "pv_gen.csv"; // normalised generation profile
#VARIABLES
internal: capacity; // capacity of solar PV plant
external: power[T]; // power output of solar PV plant
#CONSTRAINTS
capacity >= 0;
power[t] >= 0;
power[t] <= capacity_factor[t] * capacity;
#OBJECTIVES
min: capex * capacity;

#NODE BATTERY
#PARAMETERS
capex = 150; // annualised capital expenditure per unit capacity
#VARIABLES
internal: capacity; // energy capacity of battery storage system
internal: energy[T]; // energy stored in battery storage system
external: power[T]; // power flow in/out of battery storage system
#CONSTRAINTS
capacity >= 0;
energy[t] >= 0;
energy[t] <= capacity;
energy[t+1] == energy[t] + power[t];
#OBJECTIVES
min: capex * capacity;

#HYPEREDGE POWER_BALANCE
#PARAMETERS
electrical_load = import "electrical_load.csv";
#CONSTRAINTS
SOLAR_PV.power[t] == electrical_load[t] + BATTERY.power[t];
